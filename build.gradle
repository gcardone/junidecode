plugins {
  id 'application'
  id 'java'
  id 'maven'
  id 'signing'
}

group = 'net.gcardone.junidecode'
archivesBaseName = 'junidecode'
version = '0.5'
mainClassName = 'net.gcardone.junidecode.App'

repositories {
  jcenter()
}

compileJava {
  sourceCompatibility = '1.7'
  targetCompatibility = '1.7'
}

dependencies {
  testCompile 'junit:junit:4.5'
}

jar {
  manifest {
    attributes 'Main-Class': 'net.gcardone.junidecode.App'
  }
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}


uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'JUnidecode'
        packaging 'jar'
        description 'A Unicode to ASCII Java Library'
        url 'https://github.com/gcardone/junidecode'

        scm {
          connection 'scm:git:git://github.com/gcardone/junidecode.git'
          developerConnection 'scm:git:git://github.com/gcardone/junidecode.git'
          url 'https://github.com/gcardone/junidecode'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'gcardone'
            name 'Giuseppe Cardone'
            email 'ippatsuman@gmail.com'
          }
        }
      }
    }
  }
}
